pluginManagement {
	repositories {
		maven {
			url 'https://maven.neoforged.net/releases'
			content {
				includeGroupByRegex 'net\\.neoforged.*'
                includeGroupByRegex 'net\\.minecraftforge.*'
                includeGroupByRegex 'de\\.oceanlabs.*'
			}
		}

		maven {
			url 'https://maven.fabricmc.net'
			content {
				includeGroup 'net.fabricmc'
				includeGroup 'fabric-loom'
			}
		}

		maven {
			url 'https://maven.parchmentmc.org'
			content {
				includeGroupByRegex 'org\\.parchmentmc.*'
			}
		}

		maven {
			url 'https://maven.architectury.dev/'
			content {
				includeGroupByRegex 'dev\\.architectury.*'
				includeGroup 'architectury-plugin'
			}
		}

        maven {
            url 'https://libraries.minecraft.net/'
            content {
                includeGroup 'com.mojang'
            }
        }

		gradlePluginPortal()
        mavenLocal()
	}
}

plugins {
	id 'org.gradle.toolchains.foojay-resolver-convention' version '0.5.0'
}

def includeExt = { name, dir ->
	include name
	project(":${name}").projectDir = file(dir)
}

def includePlatform = { platform ->
	includeExt platform, "platforms/${platform}"
	includeExt "testmod-${platform}", "testmod/${platform}"
}

includePlatform 'common'
includePlatform 'neoforge'
includePlatform 'fabric'

rootProject.name = "${MOD_NAME}"