buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net/' }
		maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = '1.1.9'
group = 'xyz.apex.forge.apexcore'
archivesBaseName = 'ApexCore-1.16.5'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

sourceSets {
	testmod {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output

		java {
			srcDir 'src/testmod/java'
		}

		resources {
			srcDir 'src/testmod/resources'
		}
	}
}

configurations {
	testmodCompile.extendsFrom(compile)
	testmodCompileOnly.extendsFrom(compileOnly)
	testmodRuntimeOnly.extendsFrom(runtimeOnly)
}

minecraft {
	mappings channel: 'official', version: '1.16.5'
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			taskName 'Client'
			workingDirectory project.file('run/client')
			property 'fml.earlyprogresswindow', 'false'
			property 'mixin.env.disableRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
			args '-mixin.config=apexcore.mixins.json'

			if(project.hasProperty('mc_uuid') & project.hasProperty('mc_username')) {
				args '--uuid', project.getProperty('mc_uuid'), '--username', project.getProperty('mc_username')
			}

			mods {
				ApexCore {
					source sourceSets.main
				}
			}
		}

		server {
			taskName 'Server'
			workingDirectory project.file('run/server')
			property 'fml.earlyprogresswindow', 'false'
			property 'mixin.env.disableRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
			args 'nogui', '-mixin.config=apexcore.mixins.json'

			mods {
				ApexCore {
					source sourceSets.main
				}
			}
		}

		data {
			taskName 'Data'
			workingDirectory project.file('run/data')
			property 'fml.earlyprogresswindow', 'false'
			property 'mixin.env.disableRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
			args '-mixin.config=apexcore.mixins.json', '--mod', 'apexcore', '--client', '--server', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			mods {
				ApexCore {
					source sourceSets.main
				}
			}
		}

		testClient {
			parent runs.client
			taskName 'Client-TestMod'
			ideaModule "${project.name}.testmod"

			if(project.hasProperty('mc_uuid') & project.hasProperty('mc_username')) {
				args '--uuid', project.getProperty('mc_uuid'), '--username', project.getProperty('mc_username')
			}

			mods {
				TestMod {
					source sourceSets.testmod
				}
			}
		}

		testServer {
			parent runs.server
			taskName 'Server-TestMod'
			ideaModule "${project.name}.testmod"
			args 'nogui', '-mixin.config=apexcore.mixins.json'

			mods {
				TestMod {
					source sourceSets.testmod
				}
			}
		}

		testData {
			parent runs.data
			taskName 'Data-TestMod'
			ideaModule "${project.name}.testmod"
			args '-mixin.config=apexcore.mixins.json', '--mod', 'testmod', '--client', '--server', '--output', file('src/generated-testmod/resources/'), '--existing', file('src/testmod/resources/'), '--existing-mod', 'apexcore'

			mods {
				TestMod {
					source sourceSets.testmod
				}
			}
		}
	}
}

mixin {
	add sourceSets.main, 'apexcore.refmap.json'
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

sourceSets.testmod.resources {
	srcDir 'src/generated-testmod/resources'
}

repositories {
	mavenLocal() // Local Deps
	maven { url 'https://dvs1.progwml6.com/files/maven' } // JEI
	maven { url 'https://maven.tterrag.com/' } // TOP
	maven { // CurseForge
		url 'https://cursemaven.com'
		content { includeGroup 'curse.maven' }
	}

	// GitHub Packages require read access
	if(project.hasProperty('apex_git_package_token')) {
		maven {
			url = uri("https://maven.pkg.github.com/ApexModder/Registrator")
			credentials {
				username = 'Apex-GitHub-PackagesToken'
				password = project.getProperty('apex_git_package_token')
			}
		}
	} else if(System.getenv('GITHUB_TOKEN') != null) {
		// github workflows have a different set of credentials
		maven {
			url = uri("https://maven.pkg.github.com/ApexModder/Registrator")
			credentials {
				username = System.getenv('GITHUB_ACTOR')
				password = System.getenv('GITHUB_TOKEN')
			}
		}
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.16.5-36.2.5'

	// JEI
	compileOnly fg.deobf('mezz.jei:jei-1.16.5:7.6.4.90:api')
	runtimeOnly fg.deobf('mezz.jei:jei-1.16.5:7.6.4.90')

	// TOP
	compileOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.1.4-22:api')
	runtimeOnly fg.deobf('mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.1.4-22')

	// Mixin AP
	annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

	// CurseForge
	// https://www.cursemaven.com/ - Examples
	// runtimeOnly fg.deobf('curse.maven:<mod_name>-<cf_project_id>:<cf_project_file_id>')

	if(project.hasProperty('apex_local_deps') && project.getProperty('apex_local_deps')) {
		// runtimeOnly fg.deobf('xyz.apex.forge:devworld:1.16.5-2.0.0')
		runtimeOnly fg.deobf('xyz.apex.forge:smoothboot:1.16.5-1.0.0')
	}

	if(project.hasProperty('apex_git_package_token') || System.getenv('GITHUB_TOKEN') != null) {
		// we have access to Apex's GH Packages pull from there
		compileOnly fg.deobf('xyz.apex.forge.utility:registrator-1.16.5:1.0.9')
		runtimeOnly fg.deobf('xyz.apex.forge.utility:registrator-1.16.5:1.0.9')
	} else {
		// we dont have access to Apex's GH Packages, pull from CurseForge
		// TODO: Add registrator once its approved on curse forge
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allJava
}

task deobfJar(type: Jar) {
	classifier 'deobf'
	from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

jar {
	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir

	manifest {
		attributes([
				'Specification-Title': 'ApexCore',
				'Specification-Vendor': 'Apex',
				'Specification-Version': '1',
				'Implementation-Title': 'ApexCore',
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : 'Apex',
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				'MixinConfigs': 'apexcore.mixins.json'
		])
	}
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
	archives javadocJar
}

jar.finalizedBy('reobfJar')

if(System.getenv('GITHUB_TOKEN') != null) {
	publishing {
		publications {
			gpr(MavenPublication) {
				groupId = 'xyz.apex.forge'
				artifactId = 'apexcore-1.16.5'
				version = "${project.version}"

				artifact jar
				artifact sourcesJar
				artifact deobfJar
				artifact javadocJar
			}
		}
		repositories {
			maven {
				name = 'GitHubPackages'
				url = uri('https://maven.pkg.github.com/ApexModder/ApexCore')

				credentials {
					username = System.getenv('GITHUB_ACTOR')
					password = System.getenv('GITHUB_TOKEN')
				}
			}
		}
	}
}