apply plugin: 'org.groovymc.modsdotgroovy'

def neoProj
def fabricProj

if(project.name.containsIgnoreCase('testmod')) {
    if(findProject(':testmod-fabric') != null) {
        fabricProj = project(':testmod-fabric')
    }

    if(findProject(':testmod-neoforge') != null) {
        neoProj = project(':testmod-neoforge')
    }
} else {
    if(findProject(':fabric') != null) {
        fabricProj = project(':fabric')
    }

    if(findProject(':neoforge') != null) {
        neoProj = project(':neoforge')
    }
}

modsDotGroovy {
    dslVersion = '1.5.1'
    platform = 'multiloader'

    multiloader {
        common = project

        if(neoProj != null) {
            forge = [ neoProj ]
        }

        if(fabricProj != null) {
            fabric = [ fabricProj ]
        }
    }
}

sourceSets {
    main.resources.srcDirs += [ 'src/generated/resources' ]
}

architectury {
    def platforms = [ ]

    if(fabricProj != null) {
        platforms += 'fabric'
    }

    if(neoProj != null) {
        platforms += 'neoforge'
    }

    common(platforms)
}

dependencies {
    // compileOnly to not be included in maven pom
    compileOnly "org.spongepowered:mixin:${MIXIN_VERSION}"

    // MixinExtras is now included with both Fabric (0.3.0) & NeoForge (0.3.1)
    // we include 0.3.0 in common as its the lowest version compatible on both Fabric & NeoForge
    annotationProcessor 'io.github.llamalad7:mixinextras-common:0.3.0'
    implementation 'io.github.llamalad7:mixinextras-common:0.3.0'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'com.google.errorprone:error_prone_annotations:2.11.0'
}