plugins {
	id 'java-library'
	id 'idea'
	id 'maven-publish'

	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'architectury-plugin' version '3.4-SNAPSHOT'
	id 'dev.architectury.loom' version '1.0-SNAPSHOT'
	id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1'
}

evaluationDependsOn(':Common')
evaluationDependsOn(':Fabric')
evaluationDependsOn(':TestMod-Common')

group = "${APEX_GROUP}.${TEST_MOD_ID}.fabric"
version = "${TEST_MOD_VERSION}+${MINECRAFT_VERSION}"
archivesBaseName = "${TEST_MOD_ID}-fabric"

sourceSets {
	datagen {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output

		compileClasspath += sourceSets.main.compileClasspath
		runtimeClasspath += sourceSets.main.runtimeClasspath
	}
}

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	silentMojangMappingsLicense()
	accessWidenerPath = project(':TestMod-Common').loom.accessWidenerPath

	runs {
		data {
			inherit(client)
			source(sourceSets.datagen)
			vmArg('-Dfabric-api.datagen')
			vmArg("-Dfabric-api.datagen.output-dir=${project(':TestMod-Common').file('src/main/generated')}")
			vmArg("-Dfabric-api.datagen.modid=${TEST_MOD_ID}_datagen")
			vmArg('-Dfabric-api.datagen.strict-validation=true')
		}
	}

	mods {
		"${MOD_ID}" {
			sourceSet project(':Fabric').sourceSets.main
		}
	}

	createRemapConfigurations(sourceSets.datagen)
}

repositories {
	maven { url "file:///${rootProject.projectDir}/maven" }
	maven { url 'https://maven.parchmentmc.org' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url 'https://dvs1.progwml6.com/files/maven/' }
	maven { url 'https://modmaven.dev/' }
	maven { url 'https://maven.covers1624.net/' }

	maven {
		url 'https://www.cursemaven.com'
		content { includeGroup 'curse.maven' }
	}

	maven {
		url 'https://api.modrinth.com/maven'
		content { includeGroup 'maven.modrinth' }
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${MINECRAFT_VERSION}"

	mappings(loom.layered {
		officialMojangMappings()

		if(rootProject.hasProperty('PARCHMENT_MAPPINGS')) {
			def mcVersion = rootProject.hasProperty('PARCHMENT_MINECRAFT_VERSION') ? "${PARCHMENT_MINECRAFT_VERSION}" : "${MINECRAFT_VERSION}"
			parchment("org.parchmentmc.data:parchment-${mcVersion}:${PARCHMENT_MAPPINGS}@zip")
		}
	})

	// Main
	modImplementation "net.fabricmc:fabric-loader:${FABRIC_LOADER_VERSION}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${FABRIC_VERSION}+${MINECRAFT_VERSION}"
	modApi "dev.architectury:architectury-fabric:${ARCHITECTURY_VERSION}"
	common(project(path: ':Common', configuration: 'namedElements')) { transitive false }
	implementation(project(path: ':Fabric', configuration: 'namedElements')) { transitive false }
	common(project(path: ':TestMod-Common', configuration: 'namedElements')) { transitive false }
	shadowCommon(project(path: ':TestMod-Common', configuration: 'transformProductionFabric')) { transitive false }
	implementation 'com.google.code.findbugs:jsr305:3.0.2'

	// Data Gen
	modDatagenImplementation "net.fabricmc:fabric-loader:${FABRIC_LOADER_VERSION}"
	modDatagenImplementation "net.fabricmc.fabric-api:fabric-api:${FABRIC_VERSION}+${MINECRAFT_VERSION}"
	datagenImplementation(project(path: ':Fabric', configuration: 'namedElements')) { transitive false }
	// datagenImplementation 'com.google.code.findbugs:jsr305:3.0.2'

	// JEI
	if(rootProject.hasProperty('JEI_VERSION')) {
		modCompileOnlyApi "mezz.jei:jei-${MINECRAFT_VERSION}-common-api:${JEI_VERSION}"
		modCompileOnlyApi "mezz.jei:jei-${MINECRAFT_VERSION}-fabric-api:${JEI_VERSION}"
		modRuntimeOnly "mezz.jei:jei-${MINECRAFT_VERSION}-fabric:${JEI_VERSION}"
	}

	// Jade
	if(rootProject.hasProperty('JADE_CF_ID') && rootProject.hasProperty('FABRIC_JADE_CF_FILE_ID')) {
		modImplementation "curse.maven:jade-${JADE_CF_ID}:${FABRIC_JADE_CF_FILE_ID}"
	}

	// Lazy DFU
	if(rootProject.hasProperty('FABRIC_LAZY_DFU_VERSION')) {
		modRuntimeOnly "maven.modrinth:lazydfu:${FABRIC_LAZY_DFU_VERSION}"
		modDatagenRuntimeOnly "maven.modrinth:lazydfu:${FABRIC_LAZY_DFU_VERSION}"
	}

	// ModMenu
	if(rootProject.hasProperty('FABRIC_MOD_MENU_VERSION')) {
		modImplementation "com.terraformersmc:modmenu:${FABRIC_MOD_MENU_VERSION}"
	}
}

shadowJar {
	exclude 'architectury.common.json'
	configurations = [ project.configurations.shadowCommon ]
	classifier 'dev-shadow'
}

remapJar {
	injectAccessWidener = true
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	classifier null
}

processResources {
	project.properties.each { key, value ->
		inputs.property "${key}", "${value}"
	}

	filesMatching([ '*.mixins.json', 'pack.mcmeta', 'fabric.mod.json' ]) {
		expand project.properties
	}

	from project(':TestMod-Common').sourceSets.main.resources
}

processDatagenResources {
	project.properties.each { key, value ->
		inputs.property "${key}", "${value}"
	}

	filesMatching([ '*.mixins.json', 'pack.mcmeta', 'fabric.mod.json' ]) {
		expand project.properties
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.release.set(JavaLanguageVersion.of("${JAVA_VERSION}").asInt())

	javaToolchains {
		compilerFor {
			languageVersion.set(JavaLanguageVersion.of("${JAVA_VERSION}"))
		}
	}
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of("${JAVA_VERSION}"))
	}

	withSourcesJar()
}

jar {
	classifier 'dev'

	manifest {
		attributes([
				'Specification-Title': "${TEST_MOD_ID}",
				'Specification-Vendor': 'ApexStudios',
				'Specification-Version': '1',
				'Implementation-Title': "${project.name}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor': 'ApexStudios',
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

sourcesJar {
	def commonSources = project(':TestMod-Common').sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			groupId = "${project.group}"
			artifactId = "${project.archivesBaseName}"
			version = "${project.version}"

			from components.java
		}
	}
	repositories {
		maven { url "file:///${rootProject.projectDir}/maven" }

		if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
}

idea.module {
	excludeDirs << file('run')
}
